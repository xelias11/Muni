def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }


    defaultConfig {
        applicationId "cz.muni.fi.pv256.movio.uco410422"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        resConfigs "sk", "en"

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    compileOptions { //chceme psát syntaxí javy 7
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        paid {
            applicationId = "cz.muni.fi.pv256.movio.uco410422.paid"
            buildConfigField 'boolean', 'logging', 'true'
        }

        free {
            versionCode 10 //můžeme měnit na libovolné jiné číslo, než má původní verze
            versionName "10"
            applicationId = "cz.muni.fi.pv256.movio.uco410422.free"
            buildConfigField 'boolean', 'logging', 'false'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.github.siyamed:android-shape-imageview:0.9.3'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.google.code.gson:gson:2.4'
}
